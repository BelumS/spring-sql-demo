import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter
import java.time.format.FormatStyle
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    ext {
        rootPack = 'com.poc.bem.demo'
        springBootVersion = '2.3.6.RELEASE'
        logbackVersion = '1.2.3'
        swaggerVersion = '3.0.0'
        junitVersion = '5.6.0'
        lombokVersion = '1.18.14'
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0")
        classpath("org.owasp:dependency-check-gradle:6.0.3")
    }
}

plugins {
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.sonarqube' version '3.0'
    id 'org.owasp.dependencycheck' version '6.0.3'
    id 'com.palantir.docker-compose' version '0.22.1'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'com.poc.bem'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

String branch = System.getProperty("sourceBranch")

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

bootRun {
    String serverPort = System.getenv('SERVER_PORT')
    String environment = System.getenv('ENV')
    String logbackAppender = System.getenv('LOGBACK_APPENDER')
    String keyStorePw = System.getenv('KEYSTORE_PW')
    def appConfig = System.getProperty('app.config', "/tmp/config")

    def logsDir = System.getProperty('logFilePath', "/tmp/logs")
    File logsFolder = new File(logsDir)
    if (!logsFolder.exists()) {
        logsFolder.mkdirs()
    }

    File appConfigFolder = new File(appConfig)
    if (!logsFolder.exists()) {
        appConfigFolder.mkdirs()
    }

    jvmArgs = [
            "-Djava --add-opens java.base/java.lang=ALL-UNNAMED",
            "-Denvironment=$environment",
            "-Dlogging.config=classpath:logback-spring.xml",
            "-DlogFilePath=$logsDir",
            "-Dapp.config=$appConfig",
            "-Dserver.port=$serverPort",
            "-Dlogback.appender=$logbackAppender",
            "-Dwebapp.baseUrl=http://localhost:$serverPort/",
            "-Djavax.net.ssl.trustStore=$projectDir/security/api_truststore.jks",
            "-Djavax.net.ssl.keyStore=$projectDir/security/api_keystore.jks",
            "-Djavax.net.ssl.trustStorePassword=$keyStorePw",
            "-Djavax.net.ssl.KeyStorePassword=$keyStorePw"
    ]
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    //implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-tomcat'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.retry:spring-retry:1.3.0'

    implementation "ch.qos.logback:logback-core:$logbackVersion"
    implementation "ch.qos.logback:logback-classic:$logbackVersion"
    implementation "ch.qos.logback:logback-access:$logbackVersion"

    implementation 'org.apache.commons:commons-lang3:3.9'
    //implementation 'com.auth0:java-jwt:3.8.1'
    implementation "io.springfox:springfox-swagger2:$swaggerVersion"
    implementation "io.springfox:springfox-swagger-ui:$swaggerVersion"

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    runtimeOnly 'com.h2database:h2'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'org.junit.jupiter', module: 'junit-jupiter-engine'
    }

    testImplementation 'org.mockito:mockito-junit-jupiter:3.6.0'

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"

    // Additional dependencies to run Junit5 Tests in Intellij IDE
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.6.0"
    testRuntimeOnly 'org.junit.platform:junit-platform-engine:1.6.0'
}

processResources {
    filter ReplaceTokens, tokens: [
            "application.version": version.toString(),
            "application.buildTimestamp": getDate(),
            "application.branch": branch.toString(),
    ]
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "$rootPack/aop/*",
                    "$rootPack/constants/*"
            ])
        })
    }
}

dependencyCheck {
	format = 'ALL'
}

sonarqube {
    properties {
        property 'sonar.host.url', 'http://localhost:9000'
        property 'sonar.dynamicAnalysis', 'reuseReports'
        property 'sonar.junit.reportPaths', 'build/test-results/test'
        property 'sonar.jacoco.reportsPaths', 'build/test-results/test'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.java.source', '11'
        property 'sonar.java.libraries', 'build/libs'
        property 'sonar.java.binaries', 'build/classes'
        property 'sonar.java.coveragePlugin', 'jacoco'
        property 'sonar.verbose', true
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.dependencyCheck.skip', true
        property 'sonar.dependencyCheck.xmlReportPath', 'build/reports/dependency-check-report.xml'
        property 'sonar.dependencyCheck.htmlReportPath', 'build/reports/dependency-check-report.html'
        properties["sonar.sources"] += "build.gradle"
    }
}

tasks['sonarqube'].dependsOn(test)
tasks['test'].finalizedBy(jacocoTestReport)

static String getDate() {
    return ZonedDateTime.now().format(DateTimeFormatter.ofLocalizedDateTime(FormatStyle.MEDIUM))
}
